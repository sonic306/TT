#class {mage} {kill}
#class {mage} {open}

#var guild mage

#var magebar[powers][immersing] 0
#var mask 0

#NOP   HP: 2529/1261 SP: 7877/11675/94%/60% Sat: 79 Cnc: 0 G/MI: 3/1/21% ER: 0/81% G2N: 39%;
#NOP As I learned more about TT++ I realized I could have put the normal regular expression in double curly brackets instead of curling each capture. Not refactoring.;
#act {{^HP: (\d+)/(\d+) SP: (\d+)/(\d+)/(\d+)%/(\d+)% Sat: (\d+) Cnc: (\d+) G/MI: (\d+)/(\d+)/(\d+)% ER: (\d+)/(\d+)% G2N: (\d+)%}} {
	#var {sptracker[spPrevious]}	{$sptracker[spCurrent]}; 
	#var {magebar[hp][current]}	{%2};
	#var {magebar[hp][max]}		{%3};
	#var {magebar[sp][current]}	{%4};
	#var {magebar[sp][max]}		{%5};
	#var {magebar[mc]}		    {%6};
	#var {magebar[gem]}		    {%7};
	#var {magebar[sat]}		    {%8};
	#var {magebar[conc]}		{%9};
	#var {magebar[gol]}		    {%10};
	#var {magebar[imm]}		    {%11};
	#var {magebar[magreset]}	{%12};
	#var {magebar[er]}		    {%13};
	#var {magebar[screset]}		{%14};
	#var {magebar[g2n]}		    {%15};
	#var {sptracker[spCurrent]}	{$magebar[sp][current]};
	
	#IF {$sptracker[on]} {
		calculateBaseSpRegen;
	};

	#NOP About to reset. Don't let mystic immersion go to waste homie;
	#if {${magebar[magreset]} > 80 && ${magebar[imm]} > 0 && $magebar[powers][immersing] == 0} {
		#SEND !cast mystic immersion;
		#var magebar[powers][immersing] 1;
	};

	#NOP If sat is this high, then you're out of corpses. Secure shelter like a lil bitch;
	#if {${magebar[sat]} > 93} {
		#SEND !cast secure shelter;
		#SEND !ash off;
	};

	#NOP Engage mystic immersion;
	#if {${magebar[sp][current]} < 500 && ${magebar[imm]} > 0 && $magebar[powers][immersing] == 0} {
		#SHOWME !cast mystic immersion;
		#SEND !cast mystic immersion;
		#var magebar[powers][immersing] 1;
	};
	
	#NOP About to reset. Don't let explosive releases go to waste meow;
	#if {${magebar[sat]} > 90 && ${magebar[er]} > 0} {
		#SHOWME !cast explosive release;
		#SEND !cast explosive release;
	};
	
	#NOP Mystic immersions depleted if sp drops this low. Leverage eternal heal to minimize downtime;
	#NOP hasHeal is set in eternal.tin;
	#if {${magebar[sp][current]} < 200 && !${resting} && ${hasHeal}} {
		#SHOWME !(heal mikefury;
		#SEND !(heal mikefury;
	};

	#NOP Immersions gone. (heal gone. Dire situation);
	#if {${magebar[sp][current]} < 200  && ${magebar[sat]} < 85} {
		#SHOWME !hmheal, !cast spell tap;
		#SEND !hmheal;
		#SEND !cast spell tap;
	};

	#if {$magebar[gem] < 80 } {
		ffetch gem;
	};

	#if {$magebar[sat] > 85 && $magebar[cof][current] > 0} {
		unwrap;
		cast corpse blast;
	};
	#line gag;
};

#NOP  PROT: [ A MS SS M PS MG PE MB <P> ST] COF:30/30 *!!! Mon(Rip):eme;
#act {{^ PROT:.*COF:(\d+)\/(\d+)}} {
	#var {magebar[cof][current]}	{%2};
	#var {magebar[cof][max]}	    {%3};
	#line gag;
};

#alias rest {
	#VAR {resting} {1};
	#SEND !hp;
	#if {{$magebar[sp][current] < $magebar[sp][max] && $magebar[powers][immersing] == 0 && ${magebar[imm]} == 0} || {$magebar[sat] > 40 && $magebar[er] == 0 && $magebar[cof][currnet] == 0}} { 
		#SHOWME Recovering...;
		#DELAY {2} {
			rest;
		};
	}
	{
		#VAR {resting} {0};
		#showme Resuming from deep rest...;
		#if {!$singlestep} {
			..;
		};
	};
};

#act {Your immersion has expired.} {
	#var magebar[powers][immersing] 0;
}

#act {Your deeppocket begins to fade} {
	cast deeppockets;
};

#act {{Mikefury|golem} dealt the killing blow} {
   	#if {$solo} {
		#var idle 0;
		corpsetrig;		
		.bot;
	} 
	{
		look;
		#var idle 0
	}
} {2}

#act {All your corpses are already bigger than what is here} {
	cast corpse blast;
};

#alias corpsetrig {
	#var idle 0;
	#send !hp;
	#send !study spellbook;
    #if {$magebar[cof][current] == $magebar[cof][max]} {
		#if {!$mask} {
			#SEND !cast spell tap on corpse;
			#SEND !cast corpse blast;
		}
		{
			#SEND !cast spell tap on corpse;
			#SEND !cast spell tap on corpse 2;
			#SEND !cast corpse blast;
			#SEND !cast corpse blast 2;
		}
		
	} 
	{
		#if {$mask} {
			#SEND !cast spell tap on corpse;
			#SEND !cast spell tap on corpse 2;
			#SEND !wrap all;
		}
		{
			#SEND !cast spell tap on corpse;
			#SEND !wrap all;
		}
	};
    #SEND !get gold;
	put all in pocket;
	.stop;
	rest; 
};

#alias tel {
    cast telekinesis as %1 from box;
};

#alias bc {
    cast box check;
};

#alias cg {
	cast create golem %1;
	#delay {3} {cast monstrous fortitude on golem;say all, always assist;cast haste on golem};
};

#alias setcont {
		#SEND !cast perform as cast incinerate;
		#SEND !cast activate combat;
		#SEND !study spellbook;
		#SEND !cast mindblank;
		#SEND !cast find familiar;
		#SEND !cast deeppockets;
		#SEND !cast light;
		#DELAY {1} {
			#SEND !cast greater contingency as shield#cast magnificent shield;
			#SEND !cast magnificent shield;
			#DELAY {15} {
				#SEND !cast greater contingency as stoneskin#cast stoneskin;
				#SEND !cast stoneskin;
				#DELAY {15} {
					#SEND !cast greater contingency as armor#cast armor;
					#SEND !cast armor;
					#DELAY {15} {
						#SEND !cast greater contingency as major globe#cast major globe;
						#SEND !cast major globe;					
						#DELAY {15} {
							#SEND !cast greater contingency as prismatic sphere#400#cast prismatic sphere;
							#SEND !cast prismatic sphere;
							#DELAY {15} {
								#SEND !cast contingency as protection from evil#cast protection from evil;
								#SEND !cast protection from evil;
						};
					};
				};
			};
		};
	};
};


#class {mage} {close}
