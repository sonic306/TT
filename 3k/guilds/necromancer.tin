#class {necromancer} {open}

#read .tt/3k/gear.tin;
#read .tt/3k/TierTracker.tin;

#NOP --- NECROMANCER VARRS ---
#var guild necromancer
#var mask 0
#var solo 1
#var coffinmax 30
#var followermax 33
#var smugglemax 20
#var currentsmuggle 0
#var psibleedOn 0
#var corrodeOn 0
#var scytheOn 0
#var rabiesOn 0
#var convokeOn 0
#var incombat 0
#var illumeOn 0
#var canLoot 0

/* HP Bar */
#act {{HSN\[(\d+)\|(\d+)(?:\((\d+)\)|)\|(\d+)\|(\d+)\|((\d+)\%|\w+\!?)\]\s\w+\[(\d+)\%\/(\d+)c\/(\d+)c\]\s\w+\[(\d+)\|(\d+)(?:\/(\d+)\%|)\]}} {
	#var {my[hpbar][hp]}				{%2};
	#var {my[hpbar][sp]}				{%3};
	#var {my[hpbar][spfuse]}			{%4};
	#var {my[hpbar][np]}				{%5};
	#var {my[hpbar][inv_corpses]}		{%6};
	#var {my[hpbar][ehealth]}			{%7};
	#var {my[hpbar][followerhp]}		{%9};
	#var {my[hpbar][follower_corpses]}	{%10};
	#var {my[hpbar][coffin_corpses]}	{%11};
	#var {my[hpbar][teleports]}  	    {%12};
	#var {my[hpbar][glamores]}	  		{%13};
	#var {my[hpbar][glamores_burn]}	  	{%14};

	#if {$my[hpbar][np] < 20000 && $my[hpbar][coffin_corpses] >= 10}	 {
		qtrance unwrap/get corpse/absorb>15
	};

	#if {$my[hpbar][coffin_corpses] < 10 && $currentsmuggle > 0 && $my[hpbar][inv_corpses] < 10} {
		sra;
		wrap all
	};

	#if {$incombat && !$rabiesOn} {
		rabies;
		#var rabiesOn 1;
	};

	#if {$incombat && !$scytheOn} {
		scythe;
		#var scytheOn 1;
	};

	#if {$incombat && !$corrodeOn} {
		corrode;
		#var corrodeOn 1;
	};

	#if {$my[hpbar][sp] < $my[sp][max] && !$psibleedOn} {
		psibleed;
		#var psibleedOn 1;
	};

	#if {$incombat && !$convokeOn} {
		convoke ravens;
	};

	#if {$my[hpbar][np] < 70000 && $my[hpbar][glamores] > 1 && !$illumeOn} {
		illume prime;
		#var illumeOn 1;
	};

	#if {$my[hpbar][sp] < 500} {
		dream 11;
	};
}
#act {{Status\[\w(\d+)\%\|\w(\w+)\|\w(\w+)\|\w(\d+)\%\]}} {
	#var {my[hpbar][worth]}               {%2};
	#var {my[hpbar][protection_status]}   {%3};
	#var {my[hpbar][veil_status]}         {%4};
	#var {my[hpbar][power_reset]}         {%5};
	
	#if {$my[hpbar][worth] < 125} {
		con 50
	};

	#if {"$my[hpbar][protection_status]"  == "OFF"} {
		prot
	};
	
	update_status;
};

#alias {update_status} {
	#if {"$cooler" == "on" || $corpses[cooler] > 0} {
		#format {sb1_cooler} {%+2s} {$corpses[cooler]};
		#var {sb1_cooler} { CL:<130>$sb1_cooler<088>}
	} {
		#var {sb1_cooler} {}
	};
	#if {"$freezer" == "on" || $corpses[freezer] > 0} {
		#format {sb2_freezer} {%+2s} {$corpses[freezer]};
		#var {sb2_freezer} { FZ:<130>$sb2_freezer<088>}
	} {
		#var {sb2_freezer} {}
	};
	#var {my[hp][graph]}				    {@graph{$my[hp][current];$my[hp][max]}};
	#var {my[sp][graph]}				    {@graph{$my[sp][current];$my[sp][max]}};
	#var {my[gp1][graph]}				    {@graph{$my[gp1][current];$my[gp1][max]}};
	#var {enemy[graph]}					    {@graph{$enemy[hp];100}};
	#format {hp}						    {%+4s} {@colscale{$my[hp][current];$my[hp][max]}};
	#format {hp_max}					    {%+4s} {<128>$my[hp][max]<088>};
	#format {sp}						    {%+4s} {@colscale{$my[sp][current];$my[sp][max]}};
	#format {sp_inf}					    {%+3s} {<168>$my[sp][fuse][current]<088>};
	#format {sp_max}					    {%+4s} {<128>$my[sp][max]<088>};
	#format {np}						    {%+5s} {@colscale{$my[gp1][current];$my[gp1][max]}};
	#format {np_max}					    {%+5s} {<128>$my[gp1][max]<088>};
	#format {ehealth}					    {%+8s} {@colscale{$enemy[hp];100}\%};
	#format {corpses[inventory]}		    {%+2s} {$corpses[inventory]};
	#format {corpses[coffin]}			    {%+2s} {$corpses[coffin]};
	#format {corpses[freezer]}			    {%+2s} {$corpses[freezer]};
	#format {follower_corpses}   			{%+2s} {<138>$my[hpbar][follower_corpses]<088>};
	#format {powerreset} 		 		    {%+3s} {<138>$my[hpbar][power_reset]<088>};
	#format {curmuggle}						{%+2s} {<138>$currentsmuggle<088>};

	#showme {<088>HP:$hp/$hp_max SP:$sp($sp_inf)/$sp_max NP:$np/$np_max  E:$ehealth    IC:<138>${corpses[inventory]}<088>${sb1_cooler} SC:$curmuggle } {-4};
	#showme {H:$my[hp][graph] SP:$my[sp][graph]     NP:$my[gp1][graph]   E:$enemy[graph] CF:<138>$my[hpbar][coffin_corpses]<088>${sb2_freezer} FC:$follower_corpses} {-3};
	#showme {$mip[gline][1]  $mip[gline][2] Repower:$powerreset} {-6};
};

#alias {corpsetrig} {
	resetpowers;
	#if {$solo} {
		corpseme;
		get gold;
		#if {$canLoot} {cga};
		con 10;
		reinforce all;
		#var idle 0;
		#if {$bot == 1 && $mask == 0} {
			.bot;
		}
	} 
	{
		look;
		#var idle 0
	}

};


#act {{The psibleed upon .* has dissipated}} {
	#var psibleedOn 0;
};

#act {{.* has overcome the effects of your rabies}} {
	#var rabiesOn 0;
};

#act {{The corrosion upon .* has faded}} {
	#var corrodeOn 0;
};

#act {The magical illumination fades from your body} {
	#var illumeOn 0;
};

#act {The power of the sun answers your call, sending a ray of awesome light} {
	#var incombat 1;
};

#act {{The scythe attacking .* has withdrawn}} {
	scythe;
	#var scytheOn 0;
};

#act {Stormer's eyes burn with a crimson fire} {
	#var convokeOn 1;
};

#act {{The ravens peck and rend viciously upon .* one last time}} {
	#var convokeOn 0;
};

#NOP Anti Guyahi Mechanic
#act {Guyahi chants a few words and suddenly} {
	dispell queen;
};

#act {{^Coffin\s+\[\s+\d+\/\s+(\d+)\|.*}} {
	#var {coffinmax} {%2}
};

/* Preserve all */
#var preserving 0

#alias {mpp} {
	#var preserving 1;
	#var preserve_np 0;
	#var preserve_done 0;
	qtrance preserve remains>20
};

#act {You only have %d/%d necromantic points for that.} {
	#if {$preserving} {
		#var preserve_np 1
	}
};
#act {That corpse is already preserved master.} {
	#if {$preserving} {
		#var preserve_done 1
	}
};
#act {There is no corpse in this room master.} {
	#if {$preserving} {
		#var preserve_done 1
	}
};
#act {You recover your senses} {
	#if {$preserving} {
		#if {$preserve_np} {
			absorb;
			mpp
		}; #elseif {$preserve_done} {
			#var preserving 0
		}; #else {
			mpp
		}
	}
};

#act {Receiving room} {mapgo tp}

#act {Your coffin flashes as its capacity expands!} {i}

#alias corpseme {
	#if {$my[hpbar][coffin_corpses] < $coffinmax && $mask == 1} {
		qtrance preserve remains/wrap>2;
	}
	{
		#if {$my[hpbar][coffin_corpses] < $coffinmax} {
			qtrance preserve remains/wrap;
		}
	};
	#if {$my[hpbar][follower_corpses] < $followermax && $my[hpbar][coffin_corpses] == $coffinmax && $mask == 1} {
		qtrance preserve remains/=get corpse>2;
	}	{
		#if {$my[hpbar][follower_corpses] < $followermax && $my[hpbar][coffin_corpses] == $coffinmax} {
			qtrance preserve remains/=get corpse;
		}
	};
	#if {$my[hpbar][coffin_corpses] == $coffinmax && $my[hpbar][follower_corpses] == $followermax && $mask == 1 && $currentsmuggle < $smugglemax} {
		qtrance preserve remains/get corpse/smuggle corpse>2;
	}
	{
		#if {$my[hpbar][coffin_corpses] == $coffinmax && $my[hpbar][follower_corpses] == $followermax && $currentsmuggle < $smugglemax} {
			qtrance preserve remains/get corpse/smuggle corpse;
		}
	};
	#if {$my[hpbar][coffin_corpses] == $coffinmax && $my[hpbar][follower_corpses] == $followermax && $currentsmuggle == $smugglemax && $mask == 1} {
		qtrance preserve remains/get corpse>2;
	}
	{
		#if {$my[hpbar][coffin_corpses] == $coffinmax && $my[hpbar][follower_corpses] == $followermax && $currentsmuggle == $smugglemax} {
			qtrance preserve remains/get corpse;
		}		
	}
};

#alias dca {
	n;
	retrieve;
	open chest;
	get all from chest;
	s;
	donate all;
	close chest;
	deliver
};

#act {{You are currently smuggling (\d+)/20 items}} {
	#var currentsmuggle %2
};

#act {{^\w+ slaps you!!!$}} {
	qtrance preserve remains>50
};

#alias pyre {
	qtrance unwrap biggest/funeral pyre>2
};

#alias sra {
	smuggle remove all;
	#var currentsmuggle 0
};

#alias movecorpses {
	#var _temp[follower] {};
	#send !glance;
	#act {^%s%*$} {
		#if {"%%2" != ""} {
			#if {"$_temp[follower]" == ""}
            {
                #VAR {_temp[follower]} {%%2};
            };
            #else
            {
                #VAR {_temp[follower]} {$_temp[follower] %%2};
            };
		}
	} {2};
	#foreach {$_temp[follower]} {name} {
		#regex {$name} {blat} {
			#echo {Giveing Stormer a Corpse!!!}
			=give corpse
		}
	};
	#delay {3} {#unact {^%s%*$}}
};

#alias resetpowers {
	#var psibleedOn 0;
	#var corrodeOn 0;
	#var rabiesOn 0;
	#var convokeOn 0;
	#var scytheOn 0;
	#var incombat 0;
};

#act {dealt the killing blow} {#if {$mask} {#NOP} {corpsetrig}} {2}

#act {Stormer's eyes glow bright green} {corpsetrig;.bot;} {1}

#act {Stormer's eyes glow a sickly green} {corpsetrig;.bot;} {1}

#class {necromancer} {close}
